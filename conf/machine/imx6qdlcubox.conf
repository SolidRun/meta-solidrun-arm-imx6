#@TYPE: Machine
#@NAME: SolidRun i.MX6 Cubox-i & HummingBoard
#@SOC: i.MX6QDL
#@DESCRIPTION: Machine configuration for SolidRun i.MX6 Cubox-i & HummingBoard
#@MAINTAINER: Josua Mayer <josua@solid-run.com>

# This machine is compatible with i.MX6 Solo, DualLite and Quad CPUs
MACHINEOVERRIDES =. "mx6q:mx6dl"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv7a/tune-cortexa9.inc

MACHINE_FEATURES += "alsa bluetooth keyboard pci rtc screen serial usbgadget usbhost wifi"

# list device-tree compatible with this machine
KERNEL_DEVICETREE = " \
    imx6dl-cubox-i.dtb \
    imx6dl-cubox-i-emmc-som-v15.dtb \
    imx6dl-cubox-i-som-v15.dtb \
    imx6dl-hummingboard2.dtb \
    imx6dl-hummingboard2-cbi-emmc-som-v15.dtb \
    imx6dl-hummingboard2-cbi-som-v15.dtb \
    imx6dl-hummingboard2-emmc-som-v15.dtb \
    imx6dl-hummingboard2-som-v15.dtb \
    imx6dl-hummingboard.dtb \
    imx6dl-hummingboard-emmc-som-v15.dtb \
    imx6dl-hummingboard-som-v15.dtb \
    imx6dl-solidsense.dtb \
    imx6q-hummingboard2.dtb \
    imx6q-hummingboard2-cbi-emmc-som-v15.dtb \
    imx6q-hummingboard2-cbi-som-v15.dtb \
    imx6q-hummingboard2-emmc-som-v15.dtb \
    imx6q-hummingboard2-som-v15.dtb \
    imx6q-hummingboard.dtb \
    imx6q-hummingboard-emmc-som-v15.dtb \
    imx6q-hummingboard-som-v15.dtb \
    imx6q-cubox-i.dtb \
    imx6q-cubox-i-emmc-som-v15.dtb \
    imx6q-cubox-i-som-v15.dtb \
    imx6q-solidsense.dtb \
"

# include drivers & firmware
# find tmp/work/imx6qdlcubox-poky-linux-gnueabi/linux-fslc/6.0.8+git999-r0/packages-split -type f -name imx_sdma.ko
MACHINE_EXTRA_RDEPENDS:append:mx6 = " \
    kernel-module-bnep \
    kernel-module-coda-vpu \
    kernel-module-dw-hdmi-ahb-audio \
    kernel-module-evbug \
    kernel-module-gpio-ir-recv \
    kernel-module-imx-sdma \
    kernel-module-imx-vdoa \
    kernel-module-ir-imon-decoder \
    kernel-module-ir-jvc-decoder \
    kernel-module-ir-nec-decoder \
    kernel-module-ir-rc6-decoder \
    kernel-module-ir-rcmm-decoder \
    kernel-module-ir-sanyo-decoder \
    kernel-module-ir-sharp-decoder \
    kernel-module-ir-sony-decoder \
    kernel-module-ir-xmp-decoder \
    kernel-module-v4l2-jpeg \
    kernel-module-wl18xx \
    kernel-module-wlcore \
    kernel-module-wlcore-sdio \
    linux-firmware-bcm4329 \
    linux-firmware-bcm4330 \
    linux-firmware-wl18xx \
"

# default to Vendor U-Boot for
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-solidrun-imx6"
PREFERRED_PROVIDER_u-boot = "u-boot-solidrun-imx6"

# for u-boot-* package (default)
UBOOT_MACHINE = "mx6cuboxi_defconfig"
SPL_BINARY = "SPL"
UBOOT_SUFFIX = "img"

# I don't understand this, but it relates to how U-Boot is installed ...
WKS_FILE = " \
    ${@bb.utils.contains('IMX_DEFAULT_BOOTLOADER', \
                         'u-boot-fslc', 'imx-uboot-spl-bootpart.wks.in', \
                                        'imx-uboot-bootpart.wks.in', d)}"

# Use a boot.scr to allow for generating appropriate root= bootargs on the fly
UBOOT_ENV = "boot"
UBOOT_ENV_SRC_SUFFIX = "txt"
UBOOT_ENV_SUFFIX = "scr"
IMAGE_BOOT_FILES:append = "boot.scr"
