#@TYPE: Machine
#@NAME: SolidRun CuBox-i and HummingBoard
#@SOC: i.MX6 Q/DL
#@DESCRIPTION: Machine configuration for SolidRun CuBox-i and HummingBoard machines
#@MAINTAINER: Carlos Rafael Giani <dv@pseudoterminal.org>

# Machine config for the SolidRun CuBox-i and HummingBoard machines.
# They all use the same machine config, since the u-boot SPL autodetects the
# machine type upon booting. SOC_FAMILY includes all SoCs from all of these machines
# to let recipes include firmware etc. for all of these SoCs.

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa9.inc

TARGET_ARCH = "arm"
SOC_FAMILY = "mx6:mx6dl:mx6q"

PREFERRED_PROVIDER_virtual/bootloader = "u-boot-solidrun-imx6"
PREFERRED_PROVIDER_virtual/kernel = "linux-solidrun-imx6"

UBOOT_MAKE_TARGET = ""
UBOOT_SUFFIX = "img"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "mx6_cubox-i_config,sdcard"
UENV_FILENAME = "uEnv-${MACHINE}.txt"
SPL_BINARY = "SPL"

# Ensure boot scripts will be available at rootfs time
do_rootfs[depends] += "u-boot-solidrun-imx6:do_deploy"
BOOT_SCRIPTS = "${UENV_FILENAME}:uEnv.txt"

KERNEL_IMAGETYPE = "zImage"
# KERNEL_DEVICETREE = "imx6dl-cubox-i.dtb imx6q-cubox-i.dtb imx6dl-hummingboard.dtb imx6q-hummingboard.dtb imx6dl-hummingboard2.dtb imx6q-hummingboard2.dtb"
KERNEL_DEVICETREE = "imx6{q,dl}-{cubox-i,hummingboard,hummingboard2}.dtb imx6{q,dl}-{cubox-i,hummingboard,hummingboard2}-som-v15.dtb"

MACHINE_FEATURES += "pci wifi bluetooth alsa irda serial usbhost"
MACHINE_EXTRA_RRECOMMENDS += "bcm4330-nvram-config bcm4329-nvram-config"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS += " \
    kernel-image \
    kernel-devicetree \
    u-boot-solidrun-imx6 \
"

SERIAL_CONSOLE = "115200 ttymxc0"
